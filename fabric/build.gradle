architectury {
	platformSetupLoomIde()
	fabric()
}

configurations {
	developmentFabric.extendsFrom common
}

repositories {
	maven {
		url "https://maven.terraformersmc.com/releases"
	}
}

dependencies {
	modApi("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")
	modApi("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

	modApi("dev.architectury:architectury-fabric:${rootProject.architectury_version}")
	modApi("dev.latvian.mods:kubejs-fabric:${rootProject.kubejs_version}")

	modApi("com.terraformersmc:modmenu:${rootProject.fabric_mod_menu_version}")

	modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:${rootProject.rei_version}")

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	bundle(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
	def toReplace = [
			"version"             : project.version,
			"kubejs_version"      : rootProject.kubejs_version,
	]

	inputs.properties toReplace
	filesMatching("fabric.mod.json") {
		expand toReplace
	}
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = project.artifact_type
			addGameVersion "Fabric"
			addGameVersion "1.18.2"
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'architectury-api'
				requiredDependency 'kubejs'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			from components.java
		}
	}

	repositories {
		if (ENV.FTB_MAVEN_TOKEN) {
			maven {
				url "https://maven.ftb.dev/release"
				credentials {
					username = "latvian"
					password = "${ENV.FTB_MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}
