plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "com.matthewprenger.cursegradle"
}

def ENV = System.getenv()

configurations {
    shadowCommon
}

architectury {
    platformSetupLoomIde()
    fabric()
}

repositories {
    maven {
        url 'https://maven.terraformersmc.com/'
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${rootProject.minecraft_version}")
    mappings(minecraft.officialMojangMappings())
    modImplementation("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")
    modApi("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

    modApi("me.shedaniel:architectury-snapshot-fabric:${rootProject.architectury_version}")

    implementation(project(path: ":common")) {
        transitive = false
    }
    developmentFabric(project(path: ":common")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }

    modApi("curse.maven:kubejs-fabric-395864:${rootProject.fabric_kjs_file}")
    modApi("curse.maven:rhino-416294:${rootProject.rhino_file}")

    modApi("com.terraformersmc:modmenu:${rootProject.fabric_mod_menu_version}")

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api:${rootProject.fabric_rei_version}") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "net.fabricmc"
    }
    modRuntime("me.shedaniel:RoughlyEnoughItems:${rootProject.fabric_rei_version}") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "net.fabricmc"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    classifier "fabric"
}

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = project.curseforge_id_fabric
            releaseType = "release"
            addGameVersion "Fabric"
            addGameVersion "1.16.4"
            addGameVersion "1.16.5"
            mainArtifact(remapJar.archivePath)
            relations {
                requiredDependency 'kubejs-fabric'
                requiredDependency 'architectury-fabric'
                requiredDependency 'fabric-api'
            }
            changelog = getGitChangelog
            changelogType = 'markdown'
        }
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = "${rootProject.archives_base_name}-${project.name}"
            artifact(remapJar)
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "latvian"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}

rootProject.tasks.getByName("curseforgePublish").dependsOn tasks.getByName("curseforge")